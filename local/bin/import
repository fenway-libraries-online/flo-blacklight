#!/bin/zsh

typeset url='http://localhost:8983/solr/blacklight-core/update?commit=true'
typeset PROG=$0
typeset quiet=false

main() {
    if [[ $PWD != ~/proj/* ]] && [[ $PROG == ~/proj/*/local/bin/* ]]; then
        cd $PROG:h:h:h
    fi
    [[ $PWD == ~/proj/* ]] || fatal "run this from the project directory"
    typeset opt
    while getopts :i:q opt; do
        case $opt in
            (i) cat > local/inbox/$OPTARG ;;
            (q) quiet=true ;;
            (*) usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    (( $# > 0 )) || set -- local/inbox/*.json*(N)
    (( $# > 0 )) || fatal "no files to import"
    for f in $@; do
        case $f in
            (*.json.gz)
                blather "Processing $f ..."
                gunzip < $f | curl -s $url -H 'Content-type:application/json' --data-binary @- | tee /tmp/bli | report-results
                ;;
            (*.json)
                blather "Processing $f ..."
                curl -s $url -H 'Content-type:application/json' --data-binary @$f | tee /tmp/bli | report-results
                ;;
            (*) fatal "unrecognized file type: $f"
                ;;
        esac
    done
    blather 'Cleaning up...'
    rm -f $@
    blather 'Done'
}

report-results() {
    perl -ne '
        printf("OK %4dms\n", $1), exit 0
            if /^{"responseHeader":{"status":0,"QTime":(\d+)}}/;
        print(STDERR "ERR\n");
        exit 2;
    '
}

blather() {
    $quiet || print -- "$@" >&2
}

usage() {
    print "usage: $PROG:t FILE..." >&2
    exit 1
}

fatal() {
    print ${PROG:t}: "$@" >&2
    exit 2
}

main "$@"
